/**
 * Maven Publish Helper
 *
 * Requires Android Gradle plugin 3.6.0 or higher (available since Android Studio 3.6).
 * See also: https://developer.android.com/studio/build/maven-publish-plugin
 *
 * @Author Robert PÃ¶sel
 * @Version 1.5
 * @Date 3.3.2020
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Temporarily override the artifactId until it is corrected in the project
                artifactId "substrate-sdk-android"

                // Applies the component for the release build variant.
                from components.release

                // Adds javadocs and sources as separate jars.
//                artifact androidJavadocsJar
                artifact androidSourcesJar

                // You can customize attributes of the publication here or in module's build.gradle file.
                //groupId = 'com.example'
                artifactId = 'substrate-sdk-android'
                version = android.defaultConfig.versionName // or just '1.0'

                pom {
                    signing {
                        sign publishing.publications.release
                        sign configurations.archives
                    }
                    name = "substrate-sdk-android"
                    description = "Nova Substrate SDK is a native Android library to help developers build native mobile apps for Substrate-based networks, e.g. Polkadot, Kusama & parachains"
                    url = "https://github.com/nova-wallet/substrate-sdk-android"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/nova-wallet/substrate-sdk-android.git"
                        developerConnection = "scm:git:https://github.com/nova-wallet/substrate-sdk-android.git"
                        url = "https://github.com/nova-wallet/substrate-sdk-android"
                    }
                    developers {
                        developer {
                            id = "valentun"
                            name = "Valentun Sergeev"
                            email = "valentun@novawallet.io"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}
